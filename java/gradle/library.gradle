// **********************************************************************
//
// Copyright (c) 2003-2016 ZeroC, Inc. All rights reserved.
//
// This copy of Ice is licensed to you under the terms described in the
// ICE_LICENSE file included in this distribution.
//
// **********************************************************************

apply plugin: 'maven'

def pomName = "${libDir}/${project.name}-${project.version}.pom"
task writeNewPom {
    outputs.file file(pomName)
} << {
    pom {
        project {
            url 'https://zeroc.com'
            packaging 'jar'
            licenses {
                license {
                    name 'GNU General Public License, version 2'
                    url 'https://www.gnu.org/licenses/gpl-2.0.html'
                    distribution 'repo'
                }
            }
        }
    }.writeTo(pomName)
}
jar.dependsOn(writeNewPom)

jar {
    destinationDir = new File("${libDir}")
}

task jarSources(type:Jar, dependsOn: jar){
    from sourceSets.main.allSource
    classifier = 'source'
    destinationDir = new File("${libDir}")
}
assemble.dependsOn(jarSources)

clean {
    delete("${libDir}/${jar.archiveName}")
    delete("${libDir}/${project.name}-${project.version}-source.jar")
    delete(pomName)
}

task install(type: Copy, dependsOn: jarSources, overwrite: true) {
    from "${pomName}"
    from "${libDir}/${jar.archiveName}"
    from "${libDir}/${project.name}-${project.version}-source.jar"
    into "${DESTDIR}${jarDir}"
}
